/*----------------------------------------- */
/*   Copyright (C) Cogumelo Softworks - ToonKit for Cycles v1.1
/*   Directional Light
/*------------------------------------------*/

shader Cell(
    color Light = color(1,1,1),
    float Smooth = 1.,    
    float ShadowSmooth = 0.0,
    float Offset = -0.1,
    
    int SelfShadow = 1,
    
    vector Direction = vector(0, 0, 1),
    normal Normal = N,

    output float Shadow= 0,
    output color Output= 0,
    )
{   
    
    vector d = transform("world", normalize(Direction));
    float dif = dot(normalize(Normal),d);
    float value = clamp(smoothstep(Offset*(-1),Offset*(-1)+clamp(Smooth,0.0,1.0),dif),0.0,1.0);
    
    float d1 = dot(N,Direction);
    if(d1 > 0){
        Shadow = 0;
        vector disturb = ( clamp(ShadowSmooth,0.0,1.0) * (noise("perlin", P*10000.0)) );
        Shadow = (1-trace(P,Direction + disturb));

        string nameHit = "";
        getmessage("trace","geom:name",nameHit);
        
        if(startswith(nameHit,"-")){
            Shadow = 1.0;
        }
        
        if(Shadow == 0 && SelfShadow == 0){
            string name = "";
            getattribute("geom:name", name);
            int HitTrace = getmessage ("trace", "geom:name" ,  nameHit) ;
            if(nameHit== name){
                Shadow = 1.0;
            }
        }
        
    }
    
    Output = Shadow * value * Light;
}