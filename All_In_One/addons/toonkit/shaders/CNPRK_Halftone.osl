/*----------------------------------------- */
/*   Copyright (C) Cogumelo Softworks - ToonKit for Cycles v1.1
/*   Halftone Pattern
/*------------------------------------------*/


shader halftone_texture(  

    int Pattern = 0,
    vector Mapping = transform("world", "camera", P),
    float Size = 0.5,  
    float Scale = 64,
    output float Texture = 0,
    output float Halftone = 0
    
)  
{  
    float X= Mapping[0] * Scale;
    float Y= Mapping[1] * Scale;
    
    float Xa = mod(X,1);
    
    X= mod(X,1); 
    Y= mod(Y,1);

    X= X*2-1;
    Y= Y*2-1;

    int pat = clamp(Pattern,0,5);
    
    if (pat == 0)
    {
        Texture= X*X+Y*Y; /* circle */
    }
    else if (pat == 1)
    {
        Texture= max(abs(X),abs(Y)); /* square */
    } 
    else if (pat == 2)
    {
        Texture= (abs(X)+abs(Y))/2; /* diamond */
    }
    else if (pat == 3)
    {
        Texture= atan(abs(X))+atan(abs(Y));; /* cross */
    }
    else if (pat == 4)
    {
        Texture= (X+1)/2; /* linear x */
    } 
    else if (pat == 5)
    {
        Texture= (Y+1)/2; /* linear y */
    }
    if (Texture > Size) Halftone= 0; else Halftone= 1;      

}